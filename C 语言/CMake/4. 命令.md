# 命令

### 一、宏定义

> macro(<宏名> [<参数1>...])
>
>    <命令>...
>
> endmacro()
>
> 
>
> macro命令可以将其与endmacro命令之间的命令序列定义为一个名为<宏名> 的宏(macro)。宏所包含的命令序列仅在宏被调用时执行，且==执行时不会产生额外的作用域==。对于宏的行为，有一个更为形象的理解：宏就是把它所包含的命令序列直接复制到它被调用的地方来执行，因此宏本身不会拥有一个作用域，而是与调用上下文共享作用域。

```cmake

macro(testMacro x y)
    set(x ${x})
    set(y ${y})
endmacro()

testMacro(1 2)
message("x: ${x}, y: ${y}")
testMacro(3 4)
message("x: ${x}, y: ${y}")

```

```cmd
loulan@loulandeMacBook-Pro F3.9 % cmake -P 3.9.1.cmake
x: 1, y: 2
x: 3, y: 4
```



### 二、函数定义

> function(<宏名> [<参数1>...])
>
>    <命令>...
>
> endfunction()
>
> 
>
> function命令将其与endfunction命令之间的命令序列定义为一个名为<函数名> 的函数。==函数会产生一个新的作用域==，因此函数内部直接使用set命令定义的变量是不能被外部访问的。为了实现这个目的，必须为set命令指定PARENT_SCOPE参数，使得变量定义到外部作用域。

```cmake
function(testFun x y)
    set(x ${x})
    set(y ${y} PARENT_SCOPE)
endfunction()

testFun(1 2)
message("x: ${x}, y: ${y}")
```

```cmd
loulan@loulandeMacBook-Pro F3.9 % cmake -P 3.9.2.cmake
x: , y: 2
```



### 三、参数的访问

宏命令和函数除了通过 ${} 的方式来访问参数外，还可以通过列表和索引来访问参数。

> - ${ARGC} 表示参数的个数
> - ${ARGV} 表示完整的实际参数列表
> - ${ARGN} 表示和形参不对应的实际参数列表
> - ${ARGV0}、${ARGV1}、${ARGV2}……${ARGVx} 表示对应第几个参数

```cmake

macro(testM x)
    message("macro ----------")
    # 显示实际参数的个数
    message("ARGC = ${ARGC}")
    # 显示所有的实际参数
    message("ARGV = ${ARGV}")
    # 形参只有一个，那么剩余的就会在这里展示
    message("ARGN = ${ARGN}")
    # 读取对应的参数数据
    message("ARGV0 = ${ARGV0}, ARGV1 = ${ARGV1}, ARGV2 = ${ARGV2}, ARGV3 = ${ARGV3}")
endmacro()

function(testF x)
    message("function ----------")
    message("ARGC = ${ARGC}")
    message("ARGV = ${ARGV}")
    message("ARGN = ${ARGN}")
    message("ARGV0 = ${ARGV0}, ARGV1 = ${ARGV1}, ARGV2 = ${ARGV2}, ARGV3 = ${ARGV3}")
endfunction()

testM(1 2 3)
testF(1 2 3)


```

```cmd
loulan@loulandeMacBook-Pro F3.9 % cmake -P 3.9.3.cmake
macro ----------
ARGC = 3
ARGV = 1;2;3
ARGN = 2;3
ARGV0 = 1, ARGV1 = 2, ARGV2 = 3, ARGV3 = 
function ----------
ARGC = 3
ARGV = 1;2;3
ARGN = 2;3
ARGV0 = 1, ARGV1 = 2, ARGV2 = 3, ARGV3 = 
```

